buildscript {
    ext {
        kotlin_version = '1.1.3-2'
        vertx_version = '3.4.2'
        groovy_version = '2.4.7'
    }

    repositories {
        jcenter()
//    maven {
//      url "https://oss.sonatype.org/content/repositories/iovertx-3684/"
//    }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'kotlin'
apply plugin: 'groovy'

kotlin {
    experimental {
        coroutines 'enable'
    }
}

sourceCompatibility = '1.8'

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'com.aruistar.study.MainVerticle'

repositories {
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/iovertx-3684/"
    }
}
dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile "org.codehaus.groovy:groovy-all:$groovy_version"
    compile "io.vertx:vertx-core:$vertx_version"
    compile "io.vertx:vertx-web:$vertx_version"
    compile "io.vertx:vertx-mysql-postgresql-client:$vertx_version"
    compile "org.hsqldb:hsqldb:2.3.4"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.16'
}

// Redeploy watcher.
run {
    args = ['run', mainVerticleName,
            "--launcher-class=$mainClassName",
            "--redeploy=src/**/*.*",
            "--on-redeploy=./gradlew classes"
    ]
}

// Naming and packaging settings for the "shadow jar".
shadowJar {
    baseName = 'app'
    classifier = 'shadow'

    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

// Heroku relies on the 'stage' task to deploy.
task stage {
    dependsOn shadowJar
}
